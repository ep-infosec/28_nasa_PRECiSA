bbiasp_mul: THEORY
BEGIN

  IMPORTING interval_ulp_sp,
  	    float@aerr754sp_mul

  R1,E1,R2,E2: VAR Interval

  aeboundsp_mul_ia(R1,E1,R2,E2): Interval =
    If NonNeg?(E1) AND NonNeg?(E2)
    THEN Add(Add(Add(Mult(Abs(R1),E2),Mult(Abs(R2),E1)),Mult(E1,E2)),
             Div(Ulp_sp(
	          Add(
		   Mult(Abs(R1),Abs(R2)),
	           Add(Add(Mult(Abs(R1),E2),Mult(Abs(R2),E1)),Mult(E1,E2)))),
		 [|2|]))
    ELSE EmptyInterval
    ENDIF
 
  re1,re2,re3,re4: VAR RealExpr

  x1,x2,x3,x4: VAR real

  aeboundsp_mul_safe(x1,x2,x3,x4): real =
    IF x2 < 0 OR x4 < 0 THEN 1
    ELSE aeboundsp_mul(x1,x2,x3,x4) ENDIF

  AEB_MUL_inclusion_j: JUDGEMENT
    aeboundsp_mul_ia
    HAS_TYPE (Inclusion4?( Any?,
    	     		   pre1to2b(NonNeg?),
			   Any3?,
			   pre1to4d(NonNeg?),
			   aeboundsp_mul_safe ))
               
  AEB_MUL_fundmental_j: JUDGEMENT
    aeboundsp_mul_ia
    HAS_TYPE (Fundamental4?(Any?,pre1to2b(NonNeg?), Any3?,pre1to4d(NonNeg?)))

  AEB_MUL_j: JUDGEMENT
    aeboundsp_mul_ia
    HAS_TYPE {F: [[Interval, Interval, Interval, Interval] -> Interval] |
             	   Inclusion4?( Any?,
    	     		   	pre1to2b(NonNeg?),
			   	Any3?,
			   	pre1to4d(NonNeg?),
			   	aeboundsp_mul_safe ) (F)
                   AND Fundamental4?( Any?, pre1to2b(NonNeg?), Any3?,
		       		      pre1to4d(NonNeg?) ) (F)}

  AEB_MUL(re1,re2,re3,re4):  RealExpr =
    FUN4(Any?,pre1to2b(NonNeg?),Any3?,pre1to4d(NonNeg?),aeboundsp_mul_safe,
      aeboundsp_mul_ia,re1,re2,re3,re4)

END bbiasp_mul
